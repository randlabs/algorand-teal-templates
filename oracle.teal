// Oracle Example
//
// Author: Pablo Yabo
//
//
// It is a way to illustrate a bet between 2 addresses. One says that '0' will happen (team A wins)
// while the other says '1' will happen (team B wins)
// I did not code what will happen if none of these possibilities happen (e.g.: the oracle does not sign any message)
// In that case, half of the algos should go to each address after a specific round. There is an example 'split' that addresses something 
// similar that could be used to finish this example.
//
//
// Program flow:
// If the oracle signs a message with '0' 
// the algos go to TMPL_ADDRA
// If the oracle signs a '1' the algos go to TMPL_ADDRB
//
// Parameters:
//  - TMPL_ADDRA: Address beting for Team A. BKJX2U52PKL27XS42VO4SJW5BBFBYDCN7NYAW6BUEKF7EBYX5XV5CKAPT4
//  - TMPL_ADDRB: Address beting for Team B. 36WZVNA6YA77V2MGGLJBT2YQG3RQVX6UWJ3P6E7FWNAV4IGMK3ZVVNBLVQ
//  - TMPL_ADDR_ORACLE: Oracle Address. SVDICE2QVOMQPYCYRJGRBOZPLYGF5DQOK3FFHL26FUOBQLWBKDH3EAQS3U
//  - TMPL_FEE: maximum fee used by the limit order transaction. 10000


txn TypeEnum
int 1 // Payment (either Algos-out in trade or Algos redeemed at timeout)
==

global GroupSize
int 1
==
&&

// if arg 0 == 0 then send to TMPL_ADDRA
arg 0
byte base64 MA==
==

txn CloseRemainderTo
addr BKJX2U52PKL27XS42VO4SJW5BBFBYDCN7NYAW6BUEKF7EBYX5XV5CKAPT4
==
&&

txn Receiver
addr BKJX2U52PKL27XS42VO4SJW5BBFBYDCN7NYAW6BUEKF7EBYX5XV5CKAPT4
==
&&

// if arg 0 == 1 then send to TMPL_ADDRB
arg 0
byte base64 MQ==
==

txn CloseRemainderTo
addr 36WZVNA6YA77V2MGGLJBT2YQG3RQVX6UWJ3P6E7FWNAV4IGMK3ZVVNBLVQ
==
&&

txn Receiver
addr 36WZVNA6YA77V2MGGLJBT2YQG3RQVX6UWJ3P6E7FWNAV4IGMK3ZVVNBLVQ
==
&&
||
// global verifications
&&

txn Fee
int 10000
<=
&&

// verify oracle signature
arg 0
arg 1
addr SVDICE2QVOMQPYCYRJGRBOZPLYGF5DQOK3FFHL26FUOBQLWBKDH3EAQS3U
ed25519verify
&&
