// Oracle Example
//
// Author: Pablo Yabo
//
//
// It is a way to illustrate a bet between 2 addresses. One says that '0' will happen (team A wins)
// while the other says '1' will happen (team B wins)
// I did not code what will happen if none of these possibilities happen (e.g.: the oracle does not sign any message)
// In that case, half of the algos should go to each address after a specific round. There is an example 'split' that addresses something 
// similar that could be used to address this possibility.
//
//
// Program flow:
// If the oracle signs a message with '0' 
// the algos go to TMPL_ADDRA
// If the oracle signs a '1' the algos go to TMPL_ADDRB
//
// Parameters:
//  - TMPL_ADDRA: Address beting for Team A. 
//  - TMPL_ADDRB: Address beting for Team B. 
//  - TMPL_ADDR_ORACLE: Oracle Address. 
//  - TMPL_FEE: maximum fee used by the limit order transaction.


txn TypeEnum
int 1 // Payment (either Algos-out in trade or Algos redeemed at timeout)
==

global GroupSize
int 1
==
&&

// if arg 0 == 0 then send to TMPL_ADDRA
arg 0
byte base64 MA==
==

txn CloseRemainderTo
addr TMPL_ADDRA
==
&&

txn Receiver
addr TMPL_ADDRA
==
&&

// if arg 0 == 1 then send to TMPL_ADDRB
arg 0
byte base64 MQ==
==

txn CloseRemainderTo
addr TMPL_ADDRB
==
&&

txn Receiver
addr TMPL_ADDRB
==
&&
||
// global verifications
&&

txn Fee
int TMPL_FEE
<=
&&

// verify oracle signature
arg 0
arg 1
addr TMPL_ADDR_ORACLE
ed25519verify
&&
